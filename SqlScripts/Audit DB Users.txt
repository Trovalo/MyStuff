-- User Permission (Roles)
SELECT DISTINCT 
  GETDATE() as [QueryDate]
 ,@@SERVERNAME as [SqlServerInstance]
 ,DB_NAME() as [Database]
 ,u.[name] as [UserName]
 ,u.[type_desc] as [UserType]
 ,u.[default_schema_name] as [UserDefaultSchema]
 ,r.[name] as [RoleName]
 ,p.[permission_name] as [Permission]
 ,p.[class_desc] as [PermissionClass]
FROM sys.database_role_members AS m
INNER JOIN sys.database_principals AS r --Roles
 ON r.principal_id = m.role_principal_id
INNER JOIN sys.database_principals AS u --SQL User, Windows user and Win groups
 ON u.principal_id = m.member_principal_id
INNER JOIN sys.database_permissions AS p
 ON p.grantee_principal_id = r.principal_id
WHERE 
 r.[type] = 'R' 
 AND u.[type] IN ('S','U','G')
ORDER BY r.name;

-- User Permission (Direct)
SELECT DISTINCT 
  GETDATE() as [QueryDate]
 ,@@SERVERNAME as [SqlServerInstance]
 ,DB_NAME() as [Database]
 ,u.[name] as [UserName]
 ,u.[type_desc] as [UserType]
 ,u.[default_schema_name] as [UserDefaultSchema]
 ,p.[permission_name] as [Permission]
 ,p.[class_desc] as [PermissionClass]
FROM sys.database_principals AS u --SQL User, Windows user and Win groups
INNER JOIN sys.database_permissions AS p
 ON p.grantee_principal_id = u.principal_id
WHERE 
 u.[type] IN ('S','U','G')
ORDER BY u.name;

-- Login Info
SELECT
  GETDATE() as [QueryDate]
 ,@@SERVERNAME as [SqlServerInstance]
 ,DB_NAME() as [Database]
 ,sp.[name] AS [LoginName]
 ,sp.[type_desc] as [LoginType]
 ,sp.[default_database_name] as [LoginDefaultDatabase]
 ,sp.[is_disabled] as [LoginDisabled]
 ,dp.[name] AS [DatabaseUserName]
 ,dp.[create_date] AS [DatabaseUserCreateDate]
 ,dp.[modify_date] AS [DatabaseUserModifyDate]
FROM sys.database_principals AS dp
INNER JOIN sys.server_principals AS sp
 ON dp.[sid] = sp.[sid];

--Owned Schemas
SELECT
  GETDATE() as [QueryDate]
 ,@@SERVERNAME as [SqlServerInstance]
 ,DB_NAME() as [Database]
 ,dp.[name] AS [UserName]
 ,ISNULL(s.[name], 'No schema owned') AS [SchemaName]
FROM sys.database_principals AS dp
INNER JOIN sys.server_principals AS sp
 ON dp.[sid] = sp.[sid]
LEFT JOIN sys.schemas AS s
 ON s.[principal_id] = dp.[principal_id]
WHERE dp.[principal_id] > 1;

-- Role Membership
SELECT
  GETDATE() as [QueryDate]
 ,@@SERVERNAME as [SqlServerInstance]
 ,DB_NAME() as [Database]
 ,dp.[name] AS [UserName]
 ,ISNULL(r.[name], 'No roles participating') AS [RoleName]
 ,r.[is_fixed_role] as [IsFixedRole]
FROM sys.database_principals AS dp
INNER JOIN sys.server_principals AS sp
 ON dp.[sid] = sp.[sid]
LEFT JOIN sys.database_role_members AS rm
 ON rm.[member_principal_id] = dp.[principal_id]
LEFT JOIN sys.database_principals AS r
 ON r.[principal_id] = rm.[role_principal_id]
WHERE dp.[principal_id] > 1